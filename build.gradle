plugins {
	id "org.shipkit.java" version "2.0.31"
	id 'org.hidetake.swagger.generator' version '2.18.1'
}

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven'

group = 'pl.grzeslowski.jsupla.api'

dependencies {
//	swaggerCodegen 'io.swagger:swagger-codegen-cli:2.4.2'             // Swagger Codegen V2
	swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.5'  // or Swagger Codegen V3
//	swaggerCodegen 'org.openapitools:openapi-generator-cli:3.3.4'     // or OpenAPI Generator
}

// https://github.com/int128/gradle-swagger-generator-plugin
swaggerSources {
	suplaCloud {
		inputFile = file("${rootDir}/src/main/resources/openapi.yaml")
		code {
			language = 'java'
		}
	}
}
compileJava.dependsOn generateSwaggerCode
sourceSets.main.java.srcDir "${swaggerSources.suplaCloud.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.suplaCloud.code.outputDir}/src/main/resources"

repositories {
	jcenter()
	maven { url "https://dl.bintray.com/big-boy/bigboy" }
	jcenter()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

install {
	repositories.mavenInstaller {
		pom.artifactId = 'swagger-java-client'
	}
}

dependencies {
	compile 'io.swagger.core.v3:swagger-annotations:2.0.0'
	compile 'com.squareup.okhttp:okhttp:2.7.5'
	compile 'com.squareup.okhttp:logging-interceptor:2.7.5'
	compile 'com.google.code.gson:gson:2.8.1'
	compile 'io.gsonfire:gson-fire:1.8.3'
	compile 'org.threeten:threetenbp:1.3.5'
	compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

	compileOnly 'org.projectlombok:lombok:1.18.8'

	annotationProcessor 'org.projectlombok:lombok:1.18.8'

	testCompile 'junit:junit:4.12'
	testCompile "org.assertj:assertj-core:3.11.1"
	testCompile 'pl.grzeslowski.jsuplaservermock:jsupla-mock-server:0.9.13'
}

apply from: 'integration-tests.gradle'

def getOpenHabHome() {
	return project.hasProperty("OPEN_HAB_HOME") ? project.OPEN_HAB_HOME : ""
}

if (openHabHome) {
	println "  Enabling building fat jar for OpenHAb ($openHabHome)"
	task fatJar(type: Jar) {
		baseName = "${project.name}-fat"
		from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
		with jar
	}
	task openHab(type: Copy) {
		from fatJar.archivePath.absolutePath
		into "$openHabHome\\bundles\\org.openhab.binding.supla\\lib"
	}
	openHab.dependsOn fatJar
	fatJar.dependsOn build
	configure(openHab) {
		group = BasePlugin.BUILD_GROUP
	}
	configure(fatJar) {
		group = BasePlugin.BUILD_GROUP
	}
}
